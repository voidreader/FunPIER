// Toony Colors Pro+Mobile 2
// (c) 2014-2018 Jean Moreno
#NAME=SRP/Lightweight
#INFO=SRP Lightweight Template    This template will only work with the Lightweight SRP
#WARNING=This template is experimental and has been tested with:  - <b>Unity 2018.3.0b9</b> with <b>lightweight 4.1.0-preview</b>
#FEATURES
mult	lbl=Specular				kw=Off|,Color|SPECULAR_COLOR,Texture|SPECULAR_TEXTURE	toggles=SPECULAR			nohelp
sngl	lbl=Glossiness from Base Alpha		kw=SPECULAR_FROM_BASE_ALPHA						indent	needs=SPECULAR		nohelp		tt=Use Base texture alpha to mask Specular
sngl	lbl=Emission				kw=EMISSION																			nohelp		tt=Enable Emission Texture and Color properties
sngl	lbl=Normal/Bump Map			kw=BUMP			hlptop=normal_bump_map_sg											nohelp
sngl	lbl=Bump Scale				kw=BUMP_SCALE	needs=BUMP								indent						nohelp
sngl	lbl=Stylized Fresnel		kw=STYLIZED_FRESNEL																	nohelp		tt=Enable accentuated Fresnel-reflections relative to realtime lights
---
mult	lbl=Outline					kw=Off|,Opaque Outline|OUTLINE,Blended Outline|OUTLINE_BLENDING									nohelp		tt=Outline around the model
mult	lbl=Outline behind model	kw=Off|,Stencil Buffer|OUTLINE_BEHIND_STENCIL		needsOr=OUTLINE,OUTLINE_BLENDING	indent	nohelp		tt=Show outline behind model
sngl	lbl=Depth Pass				kw=OUTLINE_DEPTH	needsOr=OUTLINE,OUTLINE_BLENDING	needs=OUTLINE_BEHIND_DEPTH		indent	nohelp		tt=Adds a depth writing pass for the outline behind model: this can solve issues with sorting and drawing order
space	needs=OUTLINE_BEHIND_DEPTH	needsOr=OUTLINE,OUTLINE_BLENDING
sngl	lbl=Depth Offset			kw=OUTLINE_DEPTH_OFFSET	needsOr=OUTLINE,OUTLINE_BLENDING								indent	nohelp		tt=Adds depth buffer offset controls, can help with sorting
mult	lbl=Outline as fake rim		kw=Off|,Based on Main Directional Light|OUTLINE_FAKE_RIM_DIRLIGHT,Manual Offset|OUTLINE_FAKE_RIM		needsOr=OUTLINE,OUTLINE_BLENDING	indent	nohelp		tt=Use the outline as a fake crisp rim light
sngl	lbl=Vertex Lighting			kw=OFRD_LIGHTING	needsOr=OUTLINE,OUTLINE_BLENDING	needs=OUTLINE_FAKE_RIM_DIRLIGHT	indent	nohelp		tt=Apply basic vertex lighting to attenuate the fake rim outline color based on the directional light
sngl	lbl=Directional Light Color	kw=OFRD_COLOR		needsOr=OUTLINE,OUTLINE_BLENDING	needs=OUTLINE_FAKE_RIM_DIRLIGHT	indent	nohelp		tt=Multiply the fake rim color with the main directional light's color
space	needs=OUTLINE_FAKE_RIM_DIRLIGHT	needsOr=OUTLINE,OUTLINE_BLENDING
sngl	lbl=Vertex Color Width		kw=OUTLINE_VCOLOR_WIDTH	needsOr=OUTLINE,OUTLINE_BLENDING								indent	nohelp		tt=Modulate the outline width with a vertex color		half
keyword	lbl=Channel	kw=OVCW_CHNL	values=R|r,G|g,B|b,A|a	needsOr=OUTLINE,OUTLINE_BLENDING	needs=OUTLINE_VCOLOR_WIDTH			nohelp	default=0	forceKeyword=true								inline
sngl	lbl=Shadow Pass				kw=OUTLINE_SHADOWCASTER	needsOr=OUTLINE,OUTLINE_BLENDING								indent	nohelp		tt=Take outline into account in the shadow caster pass. This will ensure that the cast shadows include the thickness of the outline.
---
sngl	lbl=Backface Lighting			kw=USE_VFACE	nohelp		tt=Invert the normals on backfaces for accurate lighting calculation (needs to be used with the Two Sided option)
keyword	lbl=Shader Target	kw=SHADER_TARGET	nohelp	forceKeyword=true	values=2.0 (Old hardware)|2.0,2.5 (Mobile devices)|2.5,3.0 (Recommended default)|3.0,3.5|3.5,4.0|4.0,4.5|4.5,4.6|4.6,5.0|5.0		default=2
warning	msgType=info		lbl=Use <b>Shader Target 2.5</b> for maximum compatibility across mobile devices.  Increase the number if the shader fails to compile (not enough instructions or interpolators).
---
space	space=6
header	lbl=FLAGS
sngl	lbl=Disable Shadow Receiving	kw=DISABLE_SHADOW_RECEIVING	nohelp		tt=Disable shadow receiving for this shader
#flag	lbl=Disable Shadows				kw=noshadow														tt=Disables all shadow receiving support in this shader
#flag	lbl=Disable Fog					kw=nofog														tt=Disables Unity Fog support.\nCan help if you run out of vertex interpolators and don't need fog.
#flag	lbl=Disable Ambient Lighting	kw=noambient		excl=DIRAMBIENT,CUBE_AMBIENT,OCCLUSION		tt=Disable ambient lighting
#flag	lbl=Disable Vertex Lighting		kw=novertexlights												tt=Disable vertex lights and spherical harmonics (light probes)
sngl	lbl=Disable Dynamic Batching	kw=DISABLE_BATCHING		nohelp									tt=Disable dynamic batching support for this shader.  Can help if dynamic batching causes UV or vertex displacement issues among water planes for example.
#END
#KEYWORDS

#Vertex Colors usage
/// IF VCOLORS || VCOLORS_MASK
enable_kw	USE_VERTEX_COLORS
///

#END

Shader "@%SHADER_NAME%@"
{
	Properties
	{
	[TCP2Separator]
	[Header(Base)]
	[Space]
		_Color("Color", Color) = (1,1,1,1)
		_MainTex("Base (RGB) Alpha (A)", 2D) = "white" {}

	[TCP2Separator]
	[Header(Toony Colors Pro 2)]
	[Space]
		//Highlight/Shadow colors
		[HDR] _HColor("Highlight Color", Color) = (0.785,0.785,0.785,1.0)
		_SColor("Shadow Color", Color) = (0.195,0.195,0.195,1.0)
	[Space]
		//Ramp shading
		_RampThreshold("Ramp Threshold", Range(0,1)) = 0.75
		_RampSmoothness("Ramp Smoothness", Range(0,1)) = 0.01		
/// IF SPECULAR

	[TCP2Separator]
	[Header(Specular)]
	[Space]
		//Specular
		_Shininess("Shininess", Range(0.01, 10.0)) = 0.5
	/// IF SPECULAR_COLOR
		[HDR] _SpecColor("Specular Color", Color) = (1.0, 1.0, 1.0)
	/// ELIF SPECULAR_TEXTURE
		[NoScaleOffset] _SpecGlossMap("Specular Map", 2D) = "white" {}
	///
///
/// IF STYLIZED_FRESNEL

	[TCP2Separator]
	[Header(Stylized Fresnel)]
	[Space]
		//Stylized Fresnel
		_RimMin("Min", Range(0,1)) = 0.5
		_RimMax("Max", Range(0,1)) = 1.0
		_RimOffset("Offset", Range(-1,1)) = 0.0
		_RimStrength("Strength", Range(0,4)) = 0.5
///
/// IF BUMP

		//Normal mapping
	[TCP2Separator]
		[NoScaleOffset] _BumpMap("Normal Map", 2D) = "bump" {}
	/// IF BUMP_SCALE
		_BumpScale("Normal Scale", Float) = 1.0
	///

///
/// IF EMISSION

		//Emission
	[TCP2Separator]
		[NoScaleOffset] _EmissionMap("Emission Map", 2D) = "white" {}
		[HDR] _EmissionColor("Emission Color", Color) = (0,0,0)

///
/// IF OUTLINE || OUTLINE_BLENDING

	[TCP2Separator]
	[Header(Outline)]
	[Space]
		//OUTLINE
		[HDR] _OutlineColor ("Outline Color", Color) = (0.2, 0.2, 0.2, 1.0)
		_Outline ("Outline Width", Float) = 1

		//Outline Textured
		[Toggle(TCP2_OUTLINE_TEXTURED)] _EnableTexturedOutline ("Color from Texture", Float) = 0
		[TCP2KeywordFilter(TCP2_OUTLINE_TEXTURED)] _TexLod ("Texture LOD", Range(0,10)) = 5

		//Constant-size outline
		[Toggle(TCP2_OUTLINE_CONST_SIZE)] _EnableConstSizeOutline ("Constant Size Outline", Float) = 0

	/// IF OUTLINE_DEPTH_OFFSET
		//Z Offset
		_Offset1 ("Depth Offset Slope", Float) = 0
		_Offset2 ("Depth Offset Distance", Float) = 0

	///
		//This property will be ignored and will draw the custom normals GUI instead
		[TCP2OutlineNormalsGUI] __outline_gui_dummy__ ("_unused_", Float) = 0
	/// IF OUTLINE_BLENDING
		//Blending
		[TCP2Header(OUTLINE BLENDING)]
		[Enum(UnityEngine.Rendering.BlendMode)] _SrcBlendOutline ("Blending Source", Float) = 5
		[Enum(UnityEngine.Rendering.BlendMode)] _DstBlendOutline ("Blending Dest", Float) = 10
	///
	/// IF OUTLINE_BEHIND_STENCIL
		_StencilRef ("Stencil Outline Group", Range(0,255)) = 1
	///
	/// IF OUTLINE_FAKE_RIM
		_OutlineOffset ("Outline Rim Offset", Vector) = (0,0,0,0)
	///
///

		// Blending state & alpha cutoff
		[HideInInspector] _Cutoff("Alpha Cutoff", Range(0.0, 1.0)) = 0.5
		[HideInInspector] _Surface("__surface", Float) = 0.0
		[HideInInspector] _Blend("__blend", Float) = 0.0
		[HideInInspector] _AlphaClip("__clip", Float) = 0.0
		[HideInInspector] _SrcBlend("__src", Float) = 1.0
		[HideInInspector] _DstBlend("__dst", Float) = 0.0
		[HideInInspector] _ZWrite("__zw", Float) = 1.0
		[HideInInspector] _Cull("__cull", Float) = 2.0
	}

	SubShader
	{
		Tags
		{
			"RenderType" = "Opaque"
			"RenderPipeline" = "LightweightPipeline"
			"IgnoreProjector" = "True"
/// IF DISABLE_BATCHING
			"DisableBatching" = "True"
///
		}
		LOD 300

/// IF (OUTLINE || OUTLINE_BLENDING)
		//================================================================
		// OUTLINE INCLUDE

		HLSLINCLUDE
  #if defined(TCP2_OUTLINE_INCLUDE)

		#include "Packages/com.unity.render-pipelines.lightweight/ShaderLibrary/Core.hlsl"

		struct a2v
		{
			float4 vertex : POSITION;
			float3 normalOS : NORMAL;
	#if TCP2_OUTLINE_TEXTURED
			float3 texcoord : TEXCOORD0;
	#endif
	/// IF !OUTLINE_VCOLOR_WIDTH
	#if TCP2_COLORS_AS_NORMALS
	///
			float4 color : COLOR;
	/// IF !OUTLINE_VCOLOR_WIDTH
	#endif
	///
	#if TCP2_UV2_AS_NORMALS
			float2 uv2 : TEXCOORD1;
	#endif
	#if TCP2_TANGENT_AS_NORMALS
			float4 tangent : TANGENT;
	#endif
			UNITY_VERTEX_INPUT_INSTANCE_ID
		};

		struct v2f
		{
			float4 pos : SV_POSITION;
	#if TCP2_OUTLINE_TEXTURED
			float3 texlod : TEXCOORD1;
	#endif
	/// IF OUTLINE_FAKE_RIM_DIRLIGHT && OFRD_LIGHTING
			float ndl : TEXCOORD2;
	///
		};

		float _Outline;
		half4 _OutlineColor;
	/// IF OUTLINE_FAKE_RIM
		half4 _OutlineOffset;
	///

	#if TCP2_OUTLINE_TEXTURED
		sampler2D _MainTex;
		float4 _MainTex_ST;
		float _TexLod;
	#endif

	/// IF OUTLINE_FAKE_RIM_DIRLIGHT
		#define OUTLINE_WIDTH(vertex) 0.0

	/// ELSE
	  /// IF OUTLINE_VCOLOR_WIDTH
		#define OUTLINE_WIDTH(vertex) (_Outline * vertex.color.@%OVCW_CHNL%@)
	  /// ELSE
		#define OUTLINE_WIDTH(vertex) _Outline
	  ///

	///

		#ifdef TCP2_COLORS_AS_NORMALS
			#define GET_OUTLINE_NORMAL(vertex)	float3 normal = (vertex.color.xyz*2) - 1;
		#elif TCP2_TANGENT_AS_NORMALS
			#define GET_OUTLINE_NORMAL(vertex)	float3 normal = vertex.tangent.xyz;
		#elif TCP2_UV2_AS_NORMALS
			#define GET_OUTLINE_NORMAL(vertex)	\
				float3 n; \
				vertex.uv2.x = vertex.uv2.x * 255.0/16.0; \
				n.x = floor(vertex.uv2.x) / 15.0; \
				n.y = frac(vertex.uv2.x) * 16.0 / 15.0; \
				n.z = vertex.uv2.y; \
				n = n*2 - 1; \
				float3 normal = n;
		#else
			#define GET_OUTLINE_NORMAL(vertex)	float3 normal = vertex.normalOS;
		#endif

/// IF OUTLINE_FAKE_RIM_DIRLIGHT
		#define OUTLINE_SIZE	0.0
/// ELSE

	#ifdef TCP2_OUTLINE_CONST_SIZE
			//Camera-independent outline size
		#define OUTLINE_SIZE	distance(_WorldSpaceCameraPos, mul(unity_ObjectToWorld, v.vertex))
	#else
		#define OUTLINE_SIZE	1.0
	#endif
///

		v2f TCP2_Outline_Vert(a2v v)
		{
			v2f o;

			//GPU instancing support
			UNITY_SETUP_INSTANCE_ID(v);
	/// IF OUTLINE_FAKE_RIM_DIRLIGHT

			float3 objSpaceLight = mul(unity_WorldToObject, _MainLightPosition).xyz;
	#ifdef TCP2_OUTLINE_CONST_SIZE
			//Camera-independent outline size
			float dist = distance(_WorldSpaceCameraPos, mul(unity_ObjectToWorld, v.vertex).xyz);
			v.vertex.xyz += objSpaceLight.xyz * 0.01 * _Outline * dist;
	#else
			v.vertex.xyz += objSpaceLight.xyz * 0.01 * _Outline;
	#endif
		/// IF OUTLINE_FAKE_RIM_DIRLIGHT && OFRD_LIGHTING
			o.ndl = saturate(dot(v.normal.xyz, objSpaceLight.xyz) * 0.5 + 0.5);
		///
	/// ELIF OUTLINE_FAKE_RIM
			v.vertex += _OutlineOffset;
	///

			GET_OUTLINE_NORMAL(v)
			o.pos = TransformObjectToHClip(v.vertex.xyz + normal.xyz * OUTLINE_WIDTH(v) * 0.01 * OUTLINE_SIZE);

	#if TCP2_OUTLINE_TEXTURED
			half2 uv = TRANSFORM_TEX(v.texcoord, _MainTex);
			o.texlod = tex2Dlod(_MainTex, float4(uv, 0, _TexLod)).rgb;
	#endif

			return o;
		}

	/// IF OUTLINE_FAKE_RIM_DIRLIGHT && OFRD_COLOR && OFRD_LIGHTING
		#define OUTLINE_COLOR (_OutlineColor * _MainLightColor * IN.ndl)
	/// ELIF OUTLINE_FAKE_RIM_DIRLIGHT && OFRD_COLOR
		#define OUTLINE_COLOR (_OutlineColor * _MainLightColor)
	/// ELIF OUTLINE_FAKE_RIM_DIRLIGHT && OFRD_LIGHTING
		#define OUTLINE_COLOR (_OutlineColor * IN.ndl)
	/// ELSE
		#define OUTLINE_COLOR _OutlineColor
	///

		float4 TCP2_Outline_Frag (v2f IN) : SV_Target
		{
	#if TCP2_OUTLINE_TEXTURED
			return float4(IN.texlod, 1) * OUTLINE_COLOR;
	#else
			return OUTLINE_COLOR;
	#endif
		}

  #endif	//TCP2_OUTLINE_INCLUDE
		ENDHLSL

		// OUTLINE INCLUDE END
		//================================================================

///
/// IF (OUTLINE || OUTLINE_BLENDING) && OUTLINE_BEHIND_DEPTH
		//Outline
		Pass
		{
			Cull Off
			ZWrite On
/// IF OUTLINE_DEPTH_OFFSET
			Offset [_Offset1],[_Offset2]
///

	/// IF OUTLINE_BLENDING
			Tags { "Queue"="Transparent" "IgnoreProjectors"="True" "RenderType"="Transparent" }
			Blend [_SrcBlendOutline] [_DstBlendOutline]

	///
			HLSLPROGRAM
			// Required to compile gles 2.0 with standard srp library
			#pragma prefer_hlslcc gles
			#pragma target @%SHADER_TARGET%@

			//Hack to force the keyword to be set even though HLSLINCLUDE occured before this block (#define doesn't work in that case)
			#pragma multi_compile TCP2_OUTLINE_INCLUDE

			#pragma vertex TCP2_Outline_Vert
			#pragma fragment TCP2_Outline_Frag

			//--------------------------------------
			// TCP2 Outline keywords
			#pragma multi_compile _ TCP2_OUTLINE_CONST_SIZE
			#pragma multi_compile _ TCP2_COLORS_AS_NORMALS TCP2_TANGENT_AS_NORMALS TCP2_UV2_AS_NORMALS
	/// IF !FORCE_SM2
			#pragma multi_compile _ TCP2_OUTLINE_TEXTURED
	///

			//--------------------------------------
			// GPU Instancing
			#pragma multi_compile_instancing

			ENDHLSL
		}

///
/// IF (OUTLINE || OUTLINE_BLENDING) && OUTLINE_BEHIND_STENCIL
		Stencil
		{
			Ref [_StencilRef]
			Comp Always
			Pass Replace
		}

///
		Pass
		{
			Name "ForwardLit"
			Tags { "LightMode" = "LightweightForward" }

			// Use same blending / depth states as Standard shader
			Blend[_SrcBlend][_DstBlend]
			ZWrite[_ZWrite]
			Cull[_Cull]

			HLSLPROGRAM
			// Required to compile gles 2.0 with standard srp library
			#pragma prefer_hlslcc gles
			#pragma exclude_renderers d3d11_9x
			#pragma target @%SHADER_TARGET%@

			//--------------------------------------
			// Material Keywords
			#pragma shader_feature _ALPHATEST_ON
			#pragma shader_feature _ALPHAPREMULTIPLY_ON
			//#pragma shader_feature _ _SPECGLOSSMAP _SPECULAR_COLOR
			//#pragma shader_feature _GLOSSINESS_FROM_BASE_ALPHA
			//#pragma shader_feature _NORMALMAP
			//#pragma shader_feature _EMISSION
			//#pragma shader_feature _RECEIVE_SHADOWS_OFF

			//--------------------------------------
			// Lightweight Pipeline keywords
			#pragma multi_compile _ _MAIN_LIGHT_SHADOWS
			#pragma multi_compile _ _MAIN_LIGHT_SHADOWS_CASCADE
			#pragma multi_compile _ _ADDITIONAL_LIGHTS_VERTEX _ADDITIONAL_LIGHTS
			#pragma multi_compile _ _ADDITIONAL_LIGHT_SHADOWS
			#pragma multi_compile _ _SHADOWS_SOFT
			#pragma multi_compile _ _MIXED_LIGHTING_SUBTRACTIVE

			//--------------------------------------
			// Unity defined keywords
			#pragma multi_compile _ DIRLIGHTMAP_COMBINED
			#pragma multi_compile _ LIGHTMAP_ON
			#pragma multi_compile_fog

			//--------------------------------------
			// GPU Instancing
			#pragma multi_compile_instancing

			#pragma vertex LitPassVertexSimple
			#pragma fragment LitPassFragmentSimple

/// IF BUMP
			#define _NORMALMAP 1						//Enable normal mapping
	/// IF !BUMP_SCALE
			#define BUMP_SCALE_NOT_SUPPORTED 1
	///
///
/// IF EMISSION
			#define _EMISSION 1							//Enable emission
///
/// IF SPECULAR_COLOR
			#define _SPECULAR_COLOR 1					//Enable specular color
/// ELIF SPECULAR_TEXTURE
			#define _SPECGLOSSMAP 1						//Enable specular map
///
/// IF SPECULAR_FROM_BASE_ALPHA
			#define _GLOSSINESS_FROM_BASE_ALPHA 1		//Enable specular map from base alpha
///
/// IF DISABLE_SHADOW_RECEIVING
			#define _RECEIVE_SHADOWS_OFF 1				//Disable shadow receiving
///

			// SimpleLitInput.hlsl replacement

			#include "Packages/com.unity.render-pipelines.lightweight/ShaderLibrary/Core.hlsl"
			#include "Packages/com.unity.render-pipelines.lightweight/ShaderLibrary/SurfaceInput.hlsl"

			CBUFFER_START(UnityPerMaterial)
			float4 _MainTex_ST;
			half4 _Color;
			half4 _SpecColor;
			half4 _EmissionColor;
			half _Cutoff;
			half _Shininess;
			
			// TCP2 Variables
			half4 _HColor;
			half4 _SColor;
			half _RampThreshold;
			half _RampSmoothness;
/// IF STYLIZED_FRESNEL
			half _RimMin, _RimMax, _RimOffset, _RimStrength;
///
/// IF BUMP && BUMP_SCALE
			half _BumpScale;
///
			CBUFFER_END

			TEXTURE2D(_SpecGlossMap);       SAMPLER(sampler_SpecGlossMap);

			half4 SampleSpecularGloss(half2 uv, half alpha, half4 specColor, TEXTURE2D_ARGS(specGlossMap, sampler_specGlossMap))
			{
				half4 specularGloss = half4(0.0h, 0.0h, 0.0h, 1.0h);
			#ifdef _SPECGLOSSMAP
				specularGloss = SAMPLE_TEXTURE2D(specGlossMap, sampler_specGlossMap, uv);
			#elif defined(_SPECULAR_COLOR)
				specularGloss = specColor;
			#endif

			#ifdef _GLOSSINESS_FROM_BASE_ALPHA
				specularGloss.a = alpha;
			#endif
				return specularGloss;
			}
			//SimpleLitInput.hlsl end
			//--------------------------------------
			// SimpleLitForwardPass.hlsl

			#include "Packages/com.unity.render-pipelines.lightweight/ShaderLibrary/Lighting.hlsl"

			struct Attributes
			{
				float4 positionOS    : POSITION;
				float3 normalOS      : NORMAL;
				float4 tangentOS     : TANGENT;
				float2 texcoord      : TEXCOORD0;
				float2 lightmapUV    : TEXCOORD1;
				UNITY_VERTEX_INPUT_INSTANCE_ID
			};

			struct Varyings
			{
				float2 uv						: TEXCOORD0;
				DECLARE_LIGHTMAP_OR_SH(lightmapUV, vertexSH, 1);

				float4 posWSShininess			: TEXCOORD2;    // xyz: posWS, w: Shininess * 128

			#ifdef _NORMALMAP
				half4 normal					: TEXCOORD3;    // xyz: normal, w: viewDir.x
				half4 tangent					: TEXCOORD4;    // xyz: tangent, w: viewDir.y
				half4 bitangent					: TEXCOORD5;    // xyz: bitangent, w: viewDir.z
			#else
				half3  normal					: TEXCOORD3;
				half3 viewDir					: TEXCOORD4;
			#endif

				half4 fogFactorAndVertexLight	: TEXCOORD6; // x: fogFactor, yzw: vertex light

			#ifdef _MAIN_LIGHT_SHADOWS
				float4 shadowCoord				: TEXCOORD7;
			#endif

				float4 positionCS				: SV_POSITION;
				UNITY_VERTEX_INPUT_INSTANCE_ID
				UNITY_VERTEX_OUTPUT_STEREO
			};

			//--------------------------------------
			//TCP2 Lighting functions
			half3 LightingRamp(half3 lightColor, half3 lightDir, half3 normal, half attenuation, half rampThreshold, half rampSmoothness, out half NdotL)
			{
				NdotL = dot(normal, lightDir);
				half wrappedNdotL = max(0, NdotL * 0.5 + 0.5);
				NdotL = max(0, NdotL);
				half3 ramp = smoothstep(rampThreshold - rampSmoothness * 0.5, rampThreshold + rampSmoothness * 0.5, wrappedNdotL).xxx;
				return lightColor * attenuation * ramp;
			}

			half3 LightingRampColor(half3 lightColor, half3 lightDir, half3 normal, half attenuation, half rampThreshold, half rampSmoothness, half4 highlightColor, half4 shadowColor, out half NdotL)
			{
				NdotL = dot(normal, lightDir);
				half wrappedNdotL = max(0, NdotL * 0.5 + 0.5);
				NdotL = max(0, NdotL);
				half3 ramp = smoothstep(rampThreshold - rampSmoothness * 0.5, rampThreshold + rampSmoothness * 0.5, wrappedNdotL).xxx;
				ramp = lerp(shadowColor.rgb, highlightColor.rgb, ramp * attenuation);
				return lightColor * ramp;
			}

/// IF SPECULAR
			half3 LightingSpecularStylized(half3 lightColor, half3 lightDir, half3 normal, half3 viewDir, half4 specularGloss, half shininess)
			{
				half3 halfVec = SafeNormalize(lightDir + viewDir);
				half NdotH = saturate(dot(normal, halfVec));
				half modifier = pow(NdotH, shininess) * specularGloss.a;
				half3 specularReflection = specularGloss.rgb * modifier;
				return lightColor * specularReflection;
			}
///
/// IF STYLIZED_FRESNEL
			inline half3 StylizedFresnel(half3 lightColor, half3 lightDir, half3 normal, half NdotV, half rimMin, half rimMax, half rimStrength)
			{
				half rim = 1 - max(0, NdotV);
				rim = smoothstep(rimMin, rimMax, rim) * rimStrength;
				return rim * lightColor;
			}

///

			// For some reason, BUMP_SCALE_NOT_SUPPORTED doesn't work properly so we redefine this function here
			half3 SampleNormal_TCP2(float2 uv, TEXTURE2D_ARGS(bumpMap, sampler_bumpMap))
			{
			#ifdef _NORMALMAP
				half4 n = SAMPLE_TEXTURE2D(bumpMap, sampler_bumpMap, uv);
				/// IF BUMP_SCALE
					return UnpackNormalScale(n, _BumpScale);
				/// ELSE
					return UnpackNormal(n);
				///
			#else
				return half3(0.0h, 0.0h, 1.0h);
			#endif
			}

			void InitializeInputData(Varyings input, half3 normalTS, out InputData inputData)
			{
				inputData.positionWS = input.posWSShininess.xyz;

			#ifdef _NORMALMAP
				half3 viewDirWS = half3(input.normal.w, input.tangent.w, input.bitangent.w);
				inputData.normalWS = TransformTangentToWorld(normalTS,
					half3x3(input.tangent.xyz, input.bitangent.xyz, input.normal.xyz));
			#else
				half3 viewDirWS = input.viewDir;
				inputData.normalWS = input.normal;
			#endif

			#if SHADER_HINT_NICE_QUALITY
				viewDirWS = SafeNormalize(viewDirWS);
			#endif

				inputData.normalWS = NormalizeNormalPerPixel(inputData.normalWS);

				inputData.viewDirectionWS = viewDirWS;
			#if defined(_MAIN_LIGHT_SHADOWS) && !defined(_RECEIVE_SHADOWS_OFF)
				inputData.shadowCoord = input.shadowCoord;
			#else
				inputData.shadowCoord = float4(0, 0, 0, 0);
			#endif
				inputData.fogCoord = input.fogFactorAndVertexLight.x;
				inputData.vertexLighting = input.fogFactorAndVertexLight.yzw;
				inputData.bakedGI = SAMPLE_GI(input.lightmapUV, input.vertexSH, inputData.normalWS);
			}

			//===========================================================================//
			//                  Vertex and Fragment functions                            //
			//===========================================================================//

			// Used in Standard (Simple Lighting) shader
			Varyings LitPassVertexSimple(Attributes input)
			{
				Varyings output = (Varyings)0;

				UNITY_SETUP_INSTANCE_ID(input);
				UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(output);

				VertexPositionInputs vertexInput = GetVertexPositionInputs(input.positionOS.xyz);
				VertexNormalInputs normalInput = GetVertexNormalInputs(input.normalOS, input.tangentOS);
				half3 viewDirWS = GetCameraPositionWS() - vertexInput.positionWS;

			#if !SHADER_HINT_NICE_QUALITY
				viewDirWS = SafeNormalize(viewDirWS);
			#endif

				half3 vertexLight = VertexLighting(vertexInput.positionWS, normalInput.normalWS);
				half fogFactor = ComputeFogFactor(vertexInput.positionCS.z);

				output.uv = TRANSFORM_TEX(input.texcoord, _MainTex);
				output.posWSShininess.xyz = vertexInput.positionWS;
				output.posWSShininess.w = _Shininess * 128.0;
				output.positionCS = vertexInput.positionCS;

			#ifdef _NORMALMAP
				output.normal = half4(normalInput.normalWS, viewDirWS.x);
				output.tangent = half4(normalInput.tangentWS, viewDirWS.y);
				output.bitangent = half4(normalInput.bitangentWS, viewDirWS.z);
			#else
				output.normal = normalInput.normalWS;
				output.viewDir = viewDirWS;
			#endif

				OUTPUT_LIGHTMAP_UV(input.lightmapUV, unity_LightmapST, output.lightmapUV);
				OUTPUT_SH(output.normal.xyz, output.vertexSH);

				output.fogFactorAndVertexLight = half4(fogFactor, vertexLight);

			#if defined(_MAIN_LIGHT_SHADOWS) && !defined(_RECEIVE_SHADOWS_OFF)
				output.shadowCoord = GetShadowCoord(vertexInput);
			#endif

				return output;
			}

			// from Lighting.hlsl
			half4 LightweightFragmentTCP2(InputData inputData, half3 diffuse, half4 specularGloss, half shininess, half3 emission, half alpha)
			{
				Light mainLight = GetMainLight(inputData.shadowCoord);
				MixRealtimeAndBakedGI(mainLight, inputData.normalWS, inputData.bakedGI, half4(0, 0, 0, 0));

				half3 attenuatedLightColor = mainLight.color * (mainLight.distanceAttenuation * mainLight.shadowAttenuation);
				//half3 diffuseColor = inputData.bakedGI + LightingLambert(attenuatedLightColor, mainLight.direction, inputData.normalWS);
				half NdotL;
				half3 diffuseColor = inputData.bakedGI + LightingRampColor(mainLight.color, mainLight.direction, inputData.normalWS, mainLight.distanceAttenuation * mainLight.shadowAttenuation, _RampThreshold, _RampSmoothness, _HColor, _SColor, NdotL);
/// IF SPECULAR
				half3 specularColor = LightingSpecular(attenuatedLightColor, mainLight.direction, inputData.normalWS, inputData.viewDirectionWS, specularGloss, shininess);
///
/// IF STYLIZED_FRESNEL

				//Stylized rim/fresnel
				half NdotV = dot(inputData.normalWS, inputData.viewDirectionWS);
				half3 stylizedFresnel = StylizedFresnel(mainLight.color, mainLight.direction, inputData.normalWS, NdotV, _RimMin + _RimOffset, _RimMax + _RimOffset, _RimStrength) * NdotL;

///

			#ifdef _ADDITIONAL_LIGHTS
				int pixelLightCount = GetAdditionalLightsCount();
				for (int i = 0; i < pixelLightCount; ++i)
				{
					Light light = GetAdditionalLight(i, inputData.positionWS);
					half3 attenuatedLightColor = light.color * (light.distanceAttenuation * light.shadowAttenuation);
					//diffuseColor += LightingLambert(attenuatedLightColor, light.direction, inputData.normalWS);
					diffuseColor += LightingRamp(attenuatedLightColor, light.direction, inputData.normalWS, 1, _RampThreshold, _RampSmoothness, NdotL);
/// IF SPECULAR
					specularColor += LightingSpecular(attenuatedLightColor, light.direction, inputData.normalWS, inputData.viewDirectionWS, specularGloss, shininess);
///
/// IF STYLIZED_FRESNEL
					//Stylized rim/fresnel
					stylizedFresnel += StylizedFresnel(attenuatedLightColor, light.direction, inputData.normalWS, NdotV, _RimMin + _RimOffset, _RimMax + _RimOffset, _RimStrength) * NdotL;
///
				}
			#endif

			#ifdef _ADDITIONAL_LIGHTS_VERTEX
				diffuseColor += inputData.vertexLighting;
			#endif

				half3 finalColor = diffuseColor * diffuse + emission;

/// IF SPECULAR
			#if defined(_SPECGLOSSMAP) || defined(_SPECULAR_COLOR)
				finalColor += specularColor;
			#endif
///
/// IF STYLIZED_FRESNEL
				finalColor += stylizedFresnel;

///

				return half4(finalColor, alpha);
			}

			// Used for StandardSimpleLighting shader
			half4 LitPassFragmentSimple(Varyings input
/// IF USE_VFACE
										, half vFace : VFACE	//Backface lighting
///
									   ) : SV_Target
			{
				UNITY_SETUP_STEREO_EYE_INDEX_POST_VERTEX(input);

				float2 uv = input.uv;
				half4 diffuseAlpha = SampleAlbedoAlpha(uv, TEXTURE2D_PARAM(_MainTex, sampler_MainTex));
				half3 diffuse = diffuseAlpha.rgb * _Color.rgb;

				half alpha = diffuseAlpha.a * _Color.a;
				AlphaDiscard(alpha, _Cutoff);
			#ifdef _ALPHAPREMULTIPLY_ON
				diffuse *= alpha;
			#endif

				half3 normalTS = SampleNormal_TCP2(uv, TEXTURE2D_PARAM(_BumpMap, sampler_BumpMap));
				half3 emission = SampleEmission(uv, _EmissionColor.rgb, TEXTURE2D_PARAM(_EmissionMap, sampler_EmissionMap));
				half4 specularGloss = SampleSpecularGloss(uv, diffuseAlpha.a, _SpecColor, TEXTURE2D_PARAM(_SpecGlossMap, sampler_SpecGlossMap));
				half shininess = input.posWSShininess.w;

				InputData inputData;
				InitializeInputData(input, normalTS, inputData);

/// IF USE_VFACE
				//Backface lighting : flip normals for backfaces
				inputData.normalWS.xyz *= vFace;

///
				half4 color = LightweightFragmentTCP2(inputData, diffuse, specularGloss, shininess, emission, alpha);
				color.rgb = MixFog(color.rgb, inputData.fogCoord);
				return color;
			};
			// SimpleLitForwardPass.hlsl

			ENDHLSL
		}

/// IF (OUTLINE || OUTLINE_BLENDING) && !OUTLINE_BEHIND_DEPTH
		//Outline
		Pass
		{
			Cull Front
/// IF OUTLINE_DEPTH_OFFSET
			Offset [_Offset1],[_Offset2]
///
/// IF OUTLINE_BLENDING
			Tags { "Queue"="Transparent" "IgnoreProjectors"="True" "RenderType"="Transparent" }
			Blend [_SrcBlendOutline] [_DstBlendOutline]
///
	/// IF OUTLINE_BEHIND_STENCIL

			Stencil
			{
				Ref [_StencilRef]
				Comp NotEqual
				Pass Keep
			}
	///

			HLSLPROGRAM
			#pragma target @%SHADER_TARGET%@

			//Hack to force the keyword to be set even though HLSLINCLUDE occured before this block (#define doesn't work in that case)
			#pragma multi_compile TCP2_OUTLINE_INCLUDE

			#pragma vertex TCP2_Outline_Vert
			#pragma fragment TCP2_Outline_Frag

			//--------------------------------------
			// TCP2 Outline keywords
			#pragma multi_compile _ TCP2_OUTLINE_CONST_SIZE
			#pragma multi_compile _ TCP2_COLORS_AS_NORMALS TCP2_TANGENT_AS_NORMALS TCP2_UV2_AS_NORMALS
	/// IF !FORCE_SM2
			#pragma multi_compile _ TCP2_OUTLINE_TEXTURED
	///

			//--------------------------------------
			// GPU Instancing
			#pragma multi_compile_instancing
			ENDHLSL
		}

///
/// IF (OUTLINE || OUTLINE_BLENDING) && OUTLINE_BEHIND_DEPTH && OUTLINE_DEPTH

		//Outline - Depth Pass Only
		Pass
		{
			Name "OUTLINE_DEPTH"

			Cull Off
/// IF OUTLINE_DEPTH_OFFSET
			Offset [_Offset1],[_Offset2]
///

			//Write to Depth Buffer only
			ColorMask 0
			ZWrite On

			HLSLPROGRAM
			#pragma target @%SHADER_TARGET%@

			//Hack to force the keyword to be set even though HLSLINCLUDE occured before this block (#define doesn't work in that case)
			#pragma multi_compile TCP2_OUTLINE_INCLUDE

			#pragma vertex TCP2_Outline_Vert
			#pragma fragment TCP2_Outline_Frag

			//--------------------------------------
			// TCP2 Outline keywords
			#pragma multi_compile _ TCP2_OUTLINE_CONST_SIZE
			#pragma multi_compile _ TCP2_COLORS_AS_NORMALS TCP2_TANGENT_AS_NORMALS TCP2_UV2_AS_NORMALS
			//#pragma multi_compile _ TCP2_OUTLINE_TEXTURED		//Not needed for depth

			//--------------------------------------
			// GPU Instancing
			#pragma multi_compile_instancing
			ENDHLSL
		}

///
/// IF (OUTLINE || OUTLINE_BLENDING) && OUTLINE_SHADOWCASTER
		Pass
		{
			Name "ShadowCaster"
			Tags{"LightMode" = "ShadowCaster"}

			ZWrite On
			ZTest LEqual
			Cull[_Cull]

			HLSLPROGRAM
			// Required to compile gles 2.0 with standard srp library
			#pragma prefer_hlslcc gles
			#pragma exclude_renderers d3d11_9x
			#pragma target @%SHADER_TARGET%@

			// -------------------------------------
			// Material Keywords
			#pragma shader_feature _ALPHATEST_ON
			//#pragma shader_feature _GLOSSINESS_FROM_BASE_ALPHA
	/// IF SPECULAR_FROM_BASE_ALPHA
			#define _GLOSSINESS_FROM_BASE_ALPHA 1		//Enable specular map from base alpha
	///

			//--------------------------------------
			// GPU Instancing
			#pragma multi_compile_instancing

			#pragma multi_compile TCP2_OUTLINE_INCLUDE
			#pragma multi_compile _ TCP2_COLORS_AS_NORMALS TCP2_TANGENT_AS_NORMALS TCP2_UV2_AS_NORMALS

			#pragma vertex ShadowPassVertex_TCP2
			#pragma fragment ShadowPassFragment

			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/SimpleLitInput.hlsl"
			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/ShadowCasterPass.hlsl"

			//Take outline into account
			struct AttributesOutline
			{
				float3 normalOS : NORMAL;
	/// IF !OUTLINE_VCOLOR_WIDTH
		#if TCP2_COLORS_AS_NORMALS
	///
				float4 color : COLOR;
	/// IF !OUTLINE_VCOLOR_WIDTH
		#endif
	///
		#if TCP2_UV2_AS_NORMALS
				float2 uv2 : TEXCOORD1;
		#endif
		#if TCP2_TANGENT_AS_NORMALS
				float4 tangent : TANGENT;
		#endif
			};
			
			Varyings ShadowPassVertex_TCP2(Attributes input, AttributesOutline outlineInput)
			{
				Varyings output;
				UNITY_SETUP_INSTANCE_ID(input);
				output.uv = TRANSFORM_TEX(input.texcoord, _MainTex);
	/// IF OUTLINE_FAKE_RIM
				input.positionOS += _OutlineOffset;
	///
				GET_OUTLINE_NORMAL(outlineInput)
				input.positionOS.xyz += normal.xyz * OUTLINE_WIDTH(outlineInput) * 0.01;
				output.positionCS = GetShadowPositionHClip(input);
				return output;
			}

			ENDHLSL
		}

		Pass
		{
			Name "DepthOnly"
			Tags{"LightMode" = "DepthOnly"}

			ZWrite On
			ColorMask 0
			Cull[_Cull]

			HLSLPROGRAM
			// Required to compile gles 2.0 with standard srp library
			#pragma prefer_hlslcc gles
			#pragma exclude_renderers d3d11_9x
			#pragma target @%SHADER_TARGET%@

			#pragma vertex DepthOnlyVertex_TCP2
			#pragma fragment DepthOnlyFragment

			//--------------------------------------
			// Material Keywords
			#pragma shader_feature _ALPHATEST_ON
			//#pragma shader_feature _GLOSSINESS_FROM_BASE_ALPHA
	/// IF SPECULAR_FROM_BASE_ALPHA
			#define _GLOSSINESS_FROM_BASE_ALPHA 1		//Enable specular map from base alpha
	///
		
			//--------------------------------------
			// GPU Instancing
			#pragma multi_compile_instancing
			
			#pragma multi_compile TCP2_OUTLINE_INCLUDE
			#pragma multi_compile _ TCP2_COLORS_AS_NORMALS TCP2_TANGENT_AS_NORMALS TCP2_UV2_AS_NORMALS

			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/SimpleLitInput.hlsl"
			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/DepthOnlyPass.hlsl"
			
			//Take outline into account
			struct AttributesOutline
			{
				float3 normalOS : NORMAL;
	/// IF !OUTLINE_VCOLOR_WIDTH
		#if TCP2_COLORS_AS_NORMALS
	///
				float4 color : COLOR;
	/// IF !OUTLINE_VCOLOR_WIDTH
		#endif
	///
		#if TCP2_UV2_AS_NORMALS
				float2 uv2 : TEXCOORD1;
		#endif
		#if TCP2_TANGENT_AS_NORMALS
				float4 tangent : TANGENT;
		#endif
			};
			
			Varyings DepthOnlyVertex_TCP2(Attributes input, AttributesOutline outlineInput)
			{
				Varyings output = (Varyings)0;
				UNITY_SETUP_INSTANCE_ID(input);
				UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(output);

				output.uv = TRANSFORM_TEX(input.texcoord, _MainTex);
	/// IF OUTLINE_FAKE_RIM
				input.position += _OutlineOffset;
	///
				GET_OUTLINE_NORMAL(outlineInput)
				input.position.xyz += normal.xyz * OUTLINE_WIDTH(outlineInput) * 0.01;
				output.positionCS = TransformObjectToHClip(input.position.xyz);
				return output;
			}

			ENDHLSL
		}		
/// ELSE
		Pass
		{
			Name "ShadowCaster"
			Tags{"LightMode" = "ShadowCaster"}

			ZWrite On
			ZTest LEqual
			Cull[_Cull]

			HLSLPROGRAM
			// Required to compile gles 2.0 with standard srp library
			#pragma prefer_hlslcc gles
			#pragma exclude_renderers d3d11_9x
			#pragma target @%SHADER_TARGET%@

			//--------------------------------------
			// Material Keywords
			#pragma shader_feature _ALPHATEST_ON
			//#pragma shader_feature _GLOSSINESS_FROM_BASE_ALPHA
/// IF SPECULAR_FROM_BASE_ALPHA
			#define _GLOSSINESS_FROM_BASE_ALPHA 1		//Enable specular map from base alpha
///

			//--------------------------------------
			// GPU Instancing
			#pragma multi_compile_instancing

			#pragma vertex ShadowPassVertex
			#pragma fragment ShadowPassFragment

			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/SimpleLitInput.hlsl"
			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/ShadowCasterPass.hlsl"

			ENDHLSL
		}

		Pass
		{
			Name "DepthOnly"
			Tags{"LightMode" = "DepthOnly"}

			ZWrite On
			ColorMask 0
			Cull[_Cull]

			HLSLPROGRAM
			// Required to compile gles 2.0 with standard srp library
			#pragma prefer_hlslcc gles
			#pragma exclude_renderers d3d11_9x
			#pragma target @%SHADER_TARGET%@

			#pragma vertex DepthOnlyVertex
			#pragma fragment DepthOnlyFragment

			//--------------------------------------
			// Material Keywords
			#pragma shader_feature _ALPHATEST_ON
			//#pragma shader_feature _GLOSSINESS_FROM_BASE_ALPHA
/// IF SPECULAR_FROM_BASE_ALPHA
			#define _GLOSSINESS_FROM_BASE_ALPHA 1		//Enable specular map from base alpha
///
		
			//--------------------------------------
			// GPU Instancing
			#pragma multi_compile_instancing

			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/SimpleLitInput.hlsl"
			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/DepthOnlyPass.hlsl"

			ENDHLSL
		}
///

		// This pass it not used during regular rendering, only for lightmap baking.
		Pass
		{
			Name "Meta"
			Tags{ "LightMode" = "Meta" }

			Cull Off

			HLSLPROGRAM
			// Required to compile gles 2.0 with standard srp library
			#pragma prefer_hlslcc gles
			#pragma exclude_renderers d3d11_9x
			#pragma vertex LightweightVertexMeta
			#pragma fragment LightweightFragmentMetaSimple

			#pragma shader_feature _EMISSION
			#pragma shader_feature _SPECGLOSSMAP

			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/SimpleLitInput.hlsl"
			#include "Packages/com.unity.render-pipelines.lightweight/Shaders/SimpleLitMetaPass.hlsl"

			ENDHLSL
		}
	}

	Fallback "Hidden/InternalErrorShader"
	CustomEditor "TCP2_MaterialInspector_LWRP_SG"
}